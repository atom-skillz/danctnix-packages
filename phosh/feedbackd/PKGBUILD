# Maintainer: Danct12 <danct12@disroot.org>
pkgname=feedbackd
pkgver=0.8.1
pkgrel=1
pkgdesc="A daemon to provide haptic, led and audio feedback triggered by application events"
url="https://source.puri.sm/Librem5/feedbackd"
arch=('x86_64' 'armv7h' 'aarch64')
install="$pkgname.install"
license=('GPL-3.0-or-later AND LGPL-2.1-or-later')
depends=('gobject-introspection' 'gsound' 'json-glib' 'libgmobile' 'libgudev')
makedepends=('git' 'glib2-devel' 'meson' 'vala')
checkdepends=('umockdev')
_fbd_themes_name=feedbackd-device-themes
_fbd_themes_ver=0.8.1
source=(https://source.puri.sm/Librem5/$pkgname/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz
        https://source.puri.sm/Librem5/${_fbd_themes_name}/-/archive/v${_fbd_themes_ver}/${_fbd_themes_name}-v${_fbd_themes_ver}.tar.gz
        fbd-sysusers.conf)

build() {
  arch-meson $pkgname-v${pkgver} output
  ninja -C output
}

check() {
  ninja -C output test
}

package() {
  DESTDIR="$pkgdir" ninja -C output install

  install -D -m 0644 "$srcdir"/fbd-sysusers.conf "$pkgdir"/usr/lib/sysusers.d/feedbackd.conf

  sed -i 's/libexec/lib/g' "$pkgdir"/usr/lib/udev/rules.d/90-feedbackd.rules

  # It would make much more sense to bundle fbd device configuration with the pkg.
  find ${srcdir}/${_fbd_themes_name}-v${_fbd_themes_ver}/data -name \*.json \
    -exec cp {} ${pkgdir}/usr/share/feedbackd/themes \;
}

md5sums=('b3192a5d374dfda55588cedd792a9f04'
         'a1770219fc94e24adc19a5c46a76e65f'
         '44215bf5d7527f249e282408cf1d40db')
