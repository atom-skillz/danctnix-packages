# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh-mobile-settings
pkgver=0.46.0
pkgrel=1
pkgdesc="Phosh-specific settings app"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('GPL-3.0-or-later')
depends=(
    'gtk4'
    'gsound'
    'libadwaita'
    'libgmobile'
    'lm_sensors'
    'phosh'
    'wayland'
)
makedepends=(
    'git'
    'glib2-devel'
    'meson'
    'python-packaging'
    'wayland-protocols'
)
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
b2sums=('3ff025408af29ba5bc15a9ad46e0676a31ed863908f281e15b8280b4430a03de03bccf2804b1f6d6f72a892477ea0997a06994aac2d17be8c3f6ec28e028f82f'
        'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
    arch-meson $pkgname-$pkgver output
    meson compile -C output
}

package() {
    meson install -C output --skip-subprojects --destdir "$pkgdir"
}
