# Maintainer: Danct12 <danct12@disroot.org>
pkgname=xdg-desktop-portal-phosh
pkgver=0.48.0
pkgrel=1
pkgdesc="Backend implementation for xdg-desktop-portal for Phosh mobile environment"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('GPL-3.0-or-later')
depends=(
  'fontconfig'
  'glib2'
  'gnome-desktop-4'
  'gtk4'
  'libadwaita'
  'xdg-desktop-portal'
)
makedepends=(
  'glib2-devel'
  'meson'
  'rust'
)
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
b2sums=('19b720f344ba357dd1b09c74d075d02c39e385511ed3fa0dcd6f8899547ff52f700cdbbf3c7234f8eaf7b0e3161bb682ffd5973adc414fa1067ce22226ec353e'
        'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
