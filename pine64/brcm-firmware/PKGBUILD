pkgname=brcm-firmware
pkgrel=2
pkgdesc="WLAN/Bluetooth firmware files for BCM43 chips"
makedepends=('git')
arch=('any')
url="https://xff.cz/git/linux-firmware"
license=('custom')
pkgver=20250113.0228ba8
options=(!strip)
depends=('linux-firmware-broadcom')
source=("git+https://xff.cz/git/linux-firmware")
md5sums=('SKIP')

pkgver() {
  # Mimics ABS pkgver described like this:
  # Commit date + git rev-parse --short origin/master
  cd "${srcdir}/linux-firmware"
  (
  git show --format='%cI' -q master | sed 's/T.*//g;s/-//g'
  echo .
  git rev-parse --short master
  ) | tr -d '\n'
}

package() {
  mkdir -p ${pkgdir}/usr/lib/firmware/brcm
  cp -v ${srcdir}/linux-firmware/brcm/BCM43*.hcd ${pkgdir}/usr/lib/firmware/brcm
}
# vim:set ts=2 sw=2 et
