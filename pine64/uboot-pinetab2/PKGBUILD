# Maintainer: Danct12 <danct12@disroot.org>

buildarch=8

pkgname=uboot-pinetab2
pkgver=2025.07
pkgrel=1
pkgdesc="U-Boot for PineTab 2"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
depends=('uboot-tools')
makedepends=('bc' 'git' 'python' 'python-pyelftools' 'python-setuptools' 'swig')
replaces=(uboot-pinetab2-mainline)
install=${pkgname}.install
backup=("boot/boot.txt")

_commit_rkbin="f43a462e7a1429a9d407ae52b4745033034a6cf9"
source=("git+https://github.com/u-boot/u-boot.git#tag=v$pkgver"
        "rkbin::git+https://github.com/rockchip-linux/rkbin.git#commit=$_commit_rkbin"
        'vop2.patch'
        'boot.txt'
        'mkscr'
        'update-u-boot')
md5sums=('27dc84d5ba3923701eabd2bcbb2efa2b'
         '3c1f2e06bdc170f20cbc0cff196363fb'
         '630a7fa401b0f454a30baa588511784d'
         'ed65ec486f0e0dc9e9c3118dffaa3782'
         '021623a04afd29ac3f368977140cfbfd'
         'ec7c2005cb5cc9a23d1eb0e1bf089bf9')

prepare() {
  cd u-boot
  patch -p1 -i "${srcdir}/vop2.patch"
}

build() {
  cd u-boot

  make pinetab2-rk3566_defconfig

  make EXTRAVERSION=-${pkgrel} \
    BL31=../rkbin/bin/rk35/rk3568_bl31_v1.44.elf \
    ROCKCHIP_TPL=../rkbin/bin/rk35/rk3566_ddr_1056MHz_v1.23.bin
}

package() {
  cd u-boot

  mkdir -p "${pkgdir}/boot"

  cp ./idbloader.img "${pkgdir}/boot/idbloader.img"
  cp ./u-boot.itb "${pkgdir}/boot/u-boot.itb"

  cp ../{boot.txt,mkscr} "${pkgdir}"/boot
  mkdir -p "${pkgdir}"/usr/bin && cp ../update-u-boot "${pkgdir}"/usr/bin
}
