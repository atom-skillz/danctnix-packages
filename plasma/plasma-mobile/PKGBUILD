# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: dodgejcr@gmail.com
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-mobile
pkgver=6.3.5
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=1
pkgdesc="Plasma shell for mobile devices"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL-2.0-or-later AND LGPL-2.1-or-later')
depends=(
  bluez-qt
  libplasma
  kirigami-addons
  milou
  plasma-nano
  plasma-nm
  plasma-pa
  plasma-workspace=$pkgver
)
makedepends=(
  cmake
  extra-cmake-modules
)
optdepends=('hfd-service: Haptic feedback')
replaces=('plasma-phone-components')
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig})
install=$pkgname.install
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D')
sha512sums=('7cc4494c810118909f396abece697fdf6eb596de63bad20ec24c2f0665b13cba6c2564c727f919b6113dde58471fef415cf565941926d4108bcd1b076a94b666'
            'SKIP')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build -- -j1
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
